
class Main {

    function void main() {
        
        var Array grid;
        
        var String puzzle;
        
        var int index, value;
        
        let puzzle = "000000010400000000020000000000050407008000300001090000300400200050100000000806000";
        
        let index = 0;
        
        let grid = Array.new(Grid.gridWordSize());
        
        do Grid.initialise(grid);
        
        while (index < puzzle.length() & (index < 81)) {

            do Main.setValueAt(grid,
                               puzzle.charAt(index) - 48,
                               index - (index / 9 * 9),
                               index / 9);
                               
            let index = index + 1;
        }
        
        do puzzle.dispose();
        
        do Screen.clearScreen();
        
        do Output.moveCursor(0, 0);
        
        do Printer.writeGridString(grid);
        
        let value = Solver.solve(grid);
        
        do Output.println();
        
        if (value = 0) {
        
            do Printer.writeString("No solutions found");
        }
        else {
        
            if (value = 1) {
            
                do Printer.writeString("Solution is unique");
            }
            else {
            
                do Printer.writeString("Solution is not unique");
            }
        }
        
        return;
    }
    
    function void setValueAt(Array grid, int value, int x, int y) {
    
        if (x < 0 | (x > 8) | (y < 0) | (y > 8) | (value < 1) | (value > 9)) {
        
            return;
        }
    
        do Solver.setValueAt(grid, value, x, y);
    
        return;
    }
}