
class Main {

    function void main() {
        
        var Array grid;
        
        var int x, y, value;
        
        let grid = Array.new(Grid.gridWordSize());
        
        do Grid.initialise(grid);
        
        do Printer.writeGridString(grid);
        
        do Output.println();
        
        let x = Keyboard.readInt("x: ");
        
        while (x > 0) {
        
            let y = Keyboard.readInt("y: ");
            
            let value = Keyboard.readInt("value: ");
            
            do Main.setValueAt(grid, value, x - 1, y - 1);
            
            do Screen.clearScreen();
            
            do Output.moveCursor(0, 0);
            
            do Printer.writeGridString(grid);
            
            do Output.println();
            
            let x = Keyboard.readInt("x: ");
        }
        
        do Screen.clearScreen();
        
        do Output.moveCursor(0, 0);
        
        do Printer.writeGridString(grid);
        
        let value = Solver.solve(grid);
        
        do Output.println();
        
        if (value = 0) {
        
            do Output.printString("No solutions found");
        }
        else {
        
            if (value = 1) {
            
                do Output.printString("Solution is unique");
            }
            else {
            
                do Output.printString("Solution is not unique");
            }
        }
        
        return;
    }
    
    function void setValueAt(Array grid, int value, int x, int y) {
    
        if (x < 0 | (x > 8) | (y < 0) | (y > 8) | (value < 1) | (value > 9)) {
        
            return;
        }
    
        if (Grid.squareHasPossibility(grid, value, x, y)) {
        
            do Solver.setValueAt(grid, value, x, y);
        }
    
        return;
    }
}