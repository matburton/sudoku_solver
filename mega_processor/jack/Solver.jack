
class Solver {

    function void removePossibilitiesRelatedTo(Array grid, int value, int x, int y) {
    
        var int indexX, indexY, startX, startY;
        
        let indexX = 0;
        
        while (indexX < 9) {
        
            if (~(indexX = x)) {
            
                do Solver.removePossibilityAt(grid, value, indexX, y);
            }
            
            let indexX = indexX + 1;
        }
        
        let indexY = 0;
        
        while (indexY < 9) {
        
            if (~(indexY = y)) {
            
                do Solver.removePossibilityAt(grid, value, x, indexY);
            }
            
            let indexY = indexY + 1;
        }
        
        let startX = x / 3 * 3;
        let startY = y / 3 * 3;
        
        let indexY = startY;
        
        while (indexY < (startY + 3)) {
        
            let indexX = startX;
            
            while (indexX < (startX + 3)) {
            
                if (~(indexX = x | (indexY = y))) {
                
                    do Solver.removePossibilityAt(grid, value, indexX, indexY);
                }
                
                let indexX = indexX + 1;
            }
            
            let indexY = indexY + 1;
        }
    
        return;
    }
    
    function void removePossibilityAt(Array grid, int value, int x, int y) {
    
        let value = Grid.removeSquarePossibility(grid, value, x, y);
        
        if (value > 0) {
        
            do Leds.renderGridLine(grid, y);
        
            do Solver.removePossibilitiesRelatedTo(grid, value, x, y);
        }

        return;
    }
    
    function void setValueAt(Array grid, int value, int x, int y) {
    
        do Grid.setSquareValue(grid, value, x, y);
        
        do Leds.renderGridLine(grid, y);

        do Solver.removePossibilitiesRelatedTo(grid, value, x, y);

        return;
    }
    
    // Returns zero if no value could be deduced
    //
    function int getDeducedValueAt(Array grid, int x, int y) {
    
        var int value;
    
        if (Grid.getPossibilityCount(grid, x, y) > 1) {
        
            let value = 1;
            
            while (value < 10) {
            
                if (Grid.squareHasPossibility(grid, value, x, y)) {
                
                    if (Grid.mustBeValue(grid, value, x, y)) {
                    
                        return value;
                    }
                }
            
                let value = value + 1;
            }
        }
        
        return 0;
    }
    
    function void refineGrid(Array grid) {
    
        var int x, y, lastX, lastY, value;
        
        let x = 0;
        let y = 0;
        let lastX = 0;
        let lastY = 0;
        
        while (true) {
        
            let value = Solver.getDeducedValueAt(grid, x, y);
            
            if (value > 0) {
            
                do Solver.setValueAt(grid, value, x, y);
                
                if (Grid.isImpossible(grid)) { return; }
                
                let lastX = x;
                let lastY = y;
            }
            
            if (x < 8) {
            
                let x = x + 1;
            }
            else {
            
                let x = 0;
                
                if (y < 8) {
                
                    let y = y + 1;
                }
                else {
                
                    let y = 0;
                }
            }
            
            if (x = lastX & (y = lastY)) { return; }
        }
        
        return;
    }
    
    function int getAPossibilityAt(Array grid, int x, int y) {
    
        var int value;
        
        let value = 1;
        
        while (value < 10) {

            if (Grid.squareHasPossibility(grid, value, x, y)) {
            
                return value;
            }
            
            let value = value + 1;
        }
    
        return 0;
    }
    
    // Returns the number of solutions found so far
    //
    function int splitGrid(Array grid) {
    
        var Array cloneGrid;
    
        var int x, y, count, possibility, bestCount, bestX, bestY;
        
        let cloneGrid = Stack.grow(Grid.gridByteSize());
        
        let bestCount = 0;
        
        let y = 0;
        
        while (y < 9) {
        
            let x = 0;
            
            while (x < 9) {
            
                let count = Grid.getPossibilityCount(grid, x, y);
                
                if (count > 1) {
                
                    if (0 = bestCount | (count < bestCount)) {
                    
                        let bestCount = count;
                        
                        let bestX = x;
                        let bestY = y;
                    }
                }
            
                let x = x + 1;
            }
        
            let y = y + 1;
        }
        
        let possibility = Solver.getAPossibilityAt(grid, bestX, bestY);
        
        do Grid.copyFromTo(grid, cloneGrid);
        
        do Leds.addGridRenderDisable();
        
        do Solver.removePossibilityAt(grid, possibility, bestX, bestY);
        
        do Leds.undoGridRenderDisable();
        
        if (Grid.isImpossible(grid)) {
        
            do Grid.copyFromTo(cloneGrid, grid);
            
            do Solver.setValueAt(grid, possibility, bestX, bestY);
            
            return 0;
        }
        
        do Solver.setValueAt(cloneGrid, possibility, bestX, bestY);
    
        let count = Solver.solve(cloneGrid);
        
        do Leds.renderGrid(grid);
        
        return count;
    }
    
    // Returns the number of solutions found so far
    //
    function int solve(Array grid) {
    
        var int solutions;
    
        if (Grid.isImpossible(grid)) { return 0; }

        let solutions = 0;        
    
        while (solutions < 2) {
        
            do Solver.refineGrid(grid);
          
            if (Grid.isComplete(grid)) {
            
                do Leds.addGridRenderDisable();
            
                return solutions + 1;
            }
            
            if (Grid.isImpossible(grid)) { return solutions; }
            
            let solutions = Solver.splitGrid(grid);
        }
    
        return solutions;
    }
}