
        xdef    getSquarePointer
        xdef    cloneIntoGrid
        xdef    squareHasPossibility
        xdef    getPossibilityCount
        xdef    getSquareValue
        xdef    isPossible
        xdef    isComplete
        
        xref    CopyMemQuick
        
GRID_DIMENSION              equ 2
GRID_SIZE                   equ 12
GRID_SQUARE_SIZE            equ 14
GRID_IMPOSSIBLE_SQUARES     equ 16
GRID_INCOMPLETE_SQUARES     equ 18
GRID_FIRST_SQUARE           equ 20
SQUARE_POSSIBILITY_COUNT    equ 0
SQUARE_VALUE                equ 1
SQUARE_POSSIBILITY_PACK     equ 2

getSquareOffset macro
        move.w  (sp)+,d0
        move.w  (sp)+,d1
        move.l  (sp)+,a1
        mulu    GRID_DIMENSION(a1),d0
        add.w   d1,d0
        mulu    GRID_SQUARE_SIZE(a1),d0
        add.w   #\1+GRID_FIRST_SQUARE,d0
        endm

getSquarePointer
        move.l  (sp)+,a0
        getSquareOffset 0
        add.l   a1,d0
        jmp     (a0)
        
cloneIntoGrid
        move.l  (sp)+,a0
        move.l  4(sp),a1
        move.w  GRID_SIZE(a1),-(sp)
        move.w  #0,-(sp)
        move.l  a0,-(sp)
        jmp     CopyMemQuick
        
squareHasPossibility
        move.l  (sp)+,a0
        move.w  (sp)+,d1
        swap    d1
        getSquareOffset SQUARE_POSSIBILITY_PACK
        add.l   d0,a1
        clr.w   d1
        swap    d1
        subq.w  #1,d1
        move.l  d1,d0
        lsr.w   #3,d1
        add.l   d1,a1
        and.w   #8-1,d0
        btst.b  d0,(a1)
        sne     d0
        jmp     (a0)

getSquareCachedValue macro
        move.l  (sp)+,a0
        getSquareOffset \1
        add.l   d0,a1
        moveq   #0,d0
        move.b  (a1),d0
        jmp     (a0)
        endm

getPossibilityCount
        getSquareCachedValue SQUARE_POSSIBILITY_COUNT

getSquareValue
        getSquareCachedValue SQUARE_VALUE

isGridCachedValueZero macro
        movem.l (sp)+,a0/a1
        tst.w   \1(a1)
        seq     d0
        jmp     (a0)
        endm

isPossible
        isGridCachedValueZero GRID_IMPOSSIBLE_SQUARES
        
isComplete
        isGridCachedValueZero GRID_INCOMPLETE_SQUARES

end