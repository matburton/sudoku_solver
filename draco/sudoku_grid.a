
        xdef    getSquarePointer
        xdef    cloneIntoGrid
        xdef    squareHasPossibility
        xdef    getPossibilityCount
        xdef    getSquareValue
        xdef    isPossible
        xdef    isComplete

getSquareOffset macro
        moveq   #0,d0
        move.w  (sp)+,d0
        move.w  (sp)+,d1
        move.l  (sp)+,a1
        mulu    2(a1),d0
        add.w   d1,d0
        mulu    14(a1),d0
        add.w   #\1+20,d0
        endm

getSquarePointer
        move.l  (sp)+,a0
        getSquareOffset 0
        add.l   a1,d0
        jmp     (a0)
        
DMACONR equ     $dff002
DMACON  equ     $dff096
BLTCON0 equ     $dff040
BLTAMOD equ     $dff064
BLTAFWM equ     $dff044
BLTAPTH equ     $dff050
BLTDPTH equ     $dff054
BLTSIZE equ     $dff058
      
cloneIntoGrid
        move.w  #$8400,DMACON
        move.l  (sp)+,a0
        move.l  (sp)+,d0
        move.l  (sp)+,a1       
        tst     DMACONR
cigrdy  btst    #6,DMACONR
        bne.b   cigrdy
        move.l  #$09f00000,BLTCON0
        move.l  #0,BLTAMOD
        move.l  #$ffffffff,BLTAFWM
        move.l  a1,BLTAPTH
        move.l  d0,BLTDPTH
        move.w  12(a1),d1
        lsl.w   #5,d1
        addq.w  #1,d1
        move.w  d1,BLTSIZE
        move.w  #$0400,DMACON
        jmp     (a0)
        
squareHasPossibility
        move.l  (sp)+,a0
        move.w  (sp)+,d1
        swap    d1
        getSquareOffset 2
        add.l   d0,a1
        clr.w   d1
        swap    d1
        subq.w  #1,d1
        move.l  d1,d0
        lsr.w   #3,d1
        add.l   d1,a1
        and.w   #8-1,d0
        btst.b  d0,(a1)
        sne     d0
        jmp     (a0)

getSquareCachedValue macro
        move.l  (sp)+,a0
        getSquareOffset \1
        add.l   d0,a1
        moveq   #0,d0
        move.b  (a1),d0
        jmp     (a0)
        endm

getPossibilityCount
        getSquareCachedValue 0

getSquareValue
        getSquareCachedValue 1

isGridCachedValueZero macro
        movem.l (sp)+,a0/a1
        tst.w   \1+12(a1)
        seq     d0
        jmp     (a0)
        endm

isPossible
        isGridCachedValueZero 4
        
isComplete
        isGridCachedValueZero 6

end